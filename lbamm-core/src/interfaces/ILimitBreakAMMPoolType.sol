//SPDX-License-Identifier: MIT
pragma solidity 0.8.24;

import "../DataTypes.sol";

/**
 * @title  ILimitBreakAMMPoolType
 * @author Limit Break, Inc.
 * @notice Interface definition for pool types to be interacted with from the 
 *         core AMM. Pool types **DO NOT** hold funds for reserves or fees. They provide
 *         execution logic that informs the AMM core on pool creation, liquidity provisioning,
 *         swap execution and position management. Outputs from the pool type are checked against
 *         core AMM invariants on reserves and fee balances.
 */
interface ILimitBreakAMMPoolType {
    /// @dev Emitted when the pool type manifest URI is updated.
    event PoolTypeManifestUriUpdated(string uri);

    /**
     * @notice  Called by AMM core to initiate creation of a pool of the pool type, allows the pool type to
     *          store any necessary data relevant to the pool creation.
     * 
     * @dev     Developers **MUST** be aware of AMM constraints around the poolId return value
     * @dev     with regards to the pool type address and pool fee being part of the poolId.
     * 
     * @param poolCreationDetails  Pool creation parameters supplied by the pool creator.
     * 
     * @return poolId  The deterministic identifier of the created pool.
     */
    function createPool(
        PoolCreationDetails calldata poolCreationDetails
    ) external returns (bytes32 poolId);

    /**
     * @notice  Generates the poolId for the pool based on pool creation details.
     * 
     * @param poolCreationDetails  Pool creation parameters supplied by the pool creator.
     */
    function computePoolId(
        PoolCreationDetails calldata poolCreationDetails
    ) external view returns (bytes32 poolId);

    /**
     * @notice  Called by AMM core to initiate fee collection for a position in a pool of the pool type.
     * 
     * @dev     Developers **MUST** be aware of the state data that needs to be managed by the pool type 
     *          contract during fee collection operations.
     * 
     * @param poolId             Pool identifier.
     * @param provider           Address of the liquidity provider.
     * @param ammBasePositionId  Initial position identifier generated by the AMM core.
     * @param poolParams         Arbitrary calldata that may contain pool type-specific parameters.
     * 
     * @return positionId  Position identifier as managed within the pool type, for core event emissions.
     * @return fees0       Fee amount of token0.
     * @return fees1       Fee amount of token1.
     */
    function collectFees(
        bytes32 poolId,
        address provider,
        bytes32 ammBasePositionId,
        bytes calldata poolParams
    ) external returns (
        bytes32 positionId,
        uint256 fees0,
        uint256 fees1
    );

    /**
     * @notice Called by AMM core to initiate liquidity deposits for a position in a pool of the pool type.
     * 
     * @dev     Developers **MUST** be aware of the state data that needs to be managed by the pool type 
     *          contract during liquidity deposit operations.
     * 
     * @param poolId             Pool identifier.
     * @param provider           Address of the liquidity provider.
     * @param ammBasePositionId  Initial position identifier generated by the AMM core.
     * @param poolParams         Arbitrary calldata that may contain pool type-specific parameters.
     * 
     * @return positionId  Position identifier as managed within the pool type, for core event emissions.
     * @return deposit0    Amount to deposit of token0.
     * @return deposit1    Amount to deposit of token1.
     * @return fees0       Fee amount of token0.
     * @return fees1       Fee amount of token1.
     */
    function addLiquidity(
        bytes32 poolId,
        address provider,
        bytes32 ammBasePositionId,
        bytes calldata poolParams
    ) external returns (
        bytes32 positionId,
        uint256 deposit0,
        uint256 deposit1,
        uint256 fees0,
        uint256 fees1
    );

    /**
     * @notice Called by AMM core to initiate liquidity withdrawal for a position in a pool of the pool type.
     * 
     * @dev     Developers **MUST** be aware of the state data that needs to be managed by the pool type 
     *          contract during liquidity withdrawal operations.
     * 
     * @param poolId             Pool identifier.
     * @param provider           Address of the liquidity provider.
     * @param ammBasePositionId  Initial position identifier generated by the AMM core.
     * @param poolParams         Arbitrary calldata that may contain pool type-specific parameters.
     * 
     * @return positionId  Position identifier as managed within the pool type, for core event emissions.
     * @return withdraw0   Amount to withdraw of token0.
     * @return withdraw1   Amount to withdraw of token1.
     * @return fees0       Fee amount of token0.
     * @return fees1       Fee amount of token1.
     */
    function removeLiquidity(
        bytes32 poolId,
        address provider,
        bytes32 ammBasePositionId,
        bytes calldata poolParams
    ) external returns (
        bytes32 positionId,
        uint256 withdraw0,
        uint256 withdraw1,
        uint256 fees0,
        uint256 fees1
    );

    /**
     * @notice  Called by AMM core to calculate the output amount for an input-based swap on a pool of the pool type.
     * 
     * @dev     Developers **MUST** be aware of the state data that needs to be managed by the pool type
     *          contract during swapping operations.
     * 
     * @dev     Developers **MUST** provide accurate fee calculations returned to AMM core as they will be checked
     *          by core.
     * 
     * @param context         Context data for the swap from AMM core.
     * @param poolId          Pool identifier.
     * @param zeroForOne      True if swapping token0 for token1.
     * @param amountIn        Amount of input token for the swap.
     * @param poolFeeBPS      Pool fee rate in basis points.
     * @param protocolFeeBPS  Protocol fee of pool fees in basis points.
     * @param swapExtraData   Arbitrary calldata that may contain pool type-specific parameters.
     * 
     * @return actualAmountIn  Actual amount of input tokens consumed during the swap (allows for partial fill).
     * @return amountOut       Actual amount of output tokens for the swap.
     * @return feeOfAmountIn   Total fee amount of the input token for the swap.
     * @return protocolFees    Protocol fee amount of the LP fee amount.
     */
    function swapByInput(
        SwapContext calldata context,
        bytes32 poolId,
        bool zeroForOne,
        uint256 amountIn,
        uint256 poolFeeBPS,
        uint256 protocolFeeBPS,
        bytes calldata swapExtraData
    ) external returns (
        uint256 actualAmountIn,
        uint256 amountOut,
        uint256 feeOfAmountIn,
        uint256 protocolFees
    );

    /**
     * @notice  Called by AMM core to calculate the input amount for an output-based swap on a pool of the pool type.
     * 
     * @dev     Developers **MUST** be aware of the state data that needs to be managed by the pool type
     *          contract during swapping operations.
     * 
     * @dev     Developers **MUST** provide accurate fee calculations returned to AMM core as they will be checked
     *          by core.
     * 
     * @param context         Context data for the swap from AMM core.
     * @param poolId          Pool identifier.
     * @param zeroForOne      True if swapping token0 for token1.
     * @param amountOut       Amount of output token for the swap.
     * @param poolFeeBPS      Pool fee rate in basis points.
     * @param protocolFeeBPS  Protocol fee of pool fees in basis points.
     * @param swapExtraData   Arbitrary calldata that may contain pool type-specific parameters.
     * 
     * @return actualAmountOut  Actual amount of output tokens yielded by the swap (allows for partial fill).
     * @return amountIn         Actual amount of input tokens for the swap.
     * @return feeOfAmountIn    Total fee amount of the input token for the swap.
     * @return protocolFees     Protocol fee amount of the LP fee amount.
     */
    function swapByOutput(
        SwapContext calldata context,
        bytes32 poolId,
        bool zeroForOne,
        uint256 amountOut,
        uint256 poolFeeBPS,
        uint256 protocolFeeBPS,
        bytes calldata swapExtraData
    ) external returns (
        uint256 actualAmountOut,
        uint256 amountIn,
        uint256 feeOfAmountIn,
        uint256 protocolFees
    );

    /**
     * @notice  Retrieves the current price of a pool for a specific pool on an AMM.
     * 
     * @dev     Address of the AMM allows a pool type contract to operate generically servicing
     *          multiple AMMs that are compliant with this pool type interface.
     * 
     * @param amm     Address of the AMM for the pool.
     * @param poolId  Pool identifier.
     */
    function getCurrentPriceX96(
        address amm,
        bytes32 poolId
    ) external view returns (uint160 sqrtPriceX96);

    /**
     * @notice  Returns the manifest URI for the pool type to provide app integrations with
     *          information necessary to process transactions that utilize the pool type.
     * 
     * @dev     Hook developers **MUST** emit a `PoolTypeManifestUriUpdated` event if the URI
     *          changes.
     * 
     * @return  manifestUri  The URI for the hook manifest data. 
     */
    function poolTypeManifestUri() external view returns(string memory manifestUri);
}